project.ext.mainClassName = 'chire.python.PythonInterpreter'

tasks.withType(JavaCompile){
    options.encoding = "UTF-8"
    options.fork = true
    options.forkOptions.jvmArgs.addAll([
            "--add-opens", "jdk.compiler/com.sun.tools.javac.api=ALL-UNNAMED",
            "--add-opens", "jdk.compiler/com.sun.tools.javac.code=ALL-UNNAMED",
            "--add-opens", "jdk.compiler/com.sun.tools.javac.parser=ALL-UNNAMED",
            "--add-opens", "jdk.compiler/com.sun.tools.javac.processing=ALL-UNNAMED",
            "--add-opens", "jdk.compiler/com.sun.tools.javac.tree=ALL-UNNAMED",
            "--add-opens", "jdk.compiler/com.sun.tools.javac.util=ALL-UNNAMED"
    ])

    options.compilerArgs.addAll(['--release', '17'])
}

tasks.register('run', JavaExec) {
    dependsOn classes
    mainClass = mainClassName
    classpath = sourceSets.main.runtimeClasspath
    standardInput = System.in
}

tasks.register('dist', Jar) {
    dependsOn configurations.runtimeClasspath

    from files(sourceSets.main.output.classesDirs)
    from files(sourceSets.main.output.resourcesDir)
    from {configurations.runtimeClasspath.collect{ it.isDirectory() ? it : zipTree(it) }}

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    archiveFileName = "${rootProject.name}.jar"

    manifest{
        attributes 'Main-Class': mainClassName
    }
}